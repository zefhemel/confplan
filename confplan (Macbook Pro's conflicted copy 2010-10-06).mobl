application confplan

import data
import logic
import mobl::ui::generic

load confplan.css

var daysOfWeek = ["Sunday", "Monday", "Tuesday", "Wednesday", "Thursday", "Friday", "Saturday"];

screen root() {
  header("Conferences") {
    button("Init", onclick={
      loadGpce();
    })
  }
  group {
    list(conf in Conference.all()) {
      itemArrow(onclick={ conferenceDayPicker(conf); }) { label(conf.name) }
    }
  }
}

screen conferenceDayPicker(conf : Conference) {
  header(conf.name) {
    backButton()
  }
  group {
    list(day in determineDays(conf)) {
      itemArrow(onclick={ conference(conf, day); }) { label(daysOfWeek.get(day.getDay())) }
    }
  }
}

control showEvent(event : Event) {
  itemArrow() {
    floatBox(right=20) {
      <span class="dateBlob">
      label(buildTimeString(event.startDate))
      cond(event.endDate != null) {
        "-" label(buildTimeString(event.endDate))
      }
      </span>
    }
    label(event.name)
  }
}

screen conference(conf : Conference, startDate : DateTime) {
  var tomorrow = DateTime.create(startDate.getFullYear(), startDate.getMonth(), startDate.getDate() + 1)
  header(conf.name + ": " + daysOfWeek.get(startDate.getDay())) {
    backButton()
  }
  group {
    list(event in conf.events where startDate >= startDate && startDate < tomorrow order by startDate asc) {
      showEvent(event)
    }
  }
}
